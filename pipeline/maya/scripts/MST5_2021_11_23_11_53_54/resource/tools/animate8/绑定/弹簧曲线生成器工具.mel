//弹簧生成器绑定工具
//本工具仅供学习研究和个人创作使用，未经本站授权请勿商用！
//转载请注明出处<Animate8.com>！谢谢支持O(∩_∩)O~
//赋予角色生命，演绎梦想故事。
//更多精彩内容请关注<老船@动画吧>！
//网址：www.animate8.com
//QQ：149090900
//QQ群：130930540
//新浪微博：老船动画吧
////////////////////////////////////
global proc gtSpringCreateWin(){
   if(`window -ex gtSpringCreateWin`) deleteUI -window gtSpringCreateWin;
    window -wh 400 200 -t "老船角色绑定-☆-弹簧生成器绑定工具!" gtSpringCreateWin;
    columnLayout -adj 1 mainColumnattr;
    text -l "点击下方按钮↓↓↓↓↓";
    button -l "访问老船@动画吧-查看工具应用视频" -bgc 0.3 0.2 0.1 -h 45  -c "showHelp -a \"http://www.animate8.com/archives/1362\"";
        separator gtsep1;
        rowColumnLayout -nc 2 -cw 1 240 -cw 2 140;

      text -l "点击工具按钮，创建弹簧定位器\n编辑定位器弹簧生成属性";

    button -l "创建竖直弹簧"   -c "gtVehicleCurveSpringProc;" -h 40 but7;
    setParent..;
    
        separator gtsep2;
            rowColumnLayout -nc 2 -cw 1 240 -cw 2 140;

      text -l "选择曲线，沿曲线创建弹簧曲线\n编辑定位器弹簧生成属性";

    button -l "创建曲线路径弹簧"   -c "gtVehicleCurveSpringProcA;" -h 40 but8;
    setParent..;
    
        separator gtsep3;
            rowColumnLayout -nc 2 -cw 1 240 -cw 2 140;

      text -l "播放动画，弹簧曲线达到预期要求\n复制曲线，然后点击按钮删除表达式";

    button -l "删除弹簧曲线表达式"   -c "gtDeleteCurveSpringExp;" -h 40 but9;
    setParent..;
    
        separator gtsep4;
   
    showWindow gtSpringCreateWin;   
}
/////////////////////////
global proc gtDeleteCurveSpringExp(){
select -r "*gtSpringCurveExp";
string $expNode[]=`ls -sl -type "expression"`;
delete $expNode;
}
////////////////////////////
global proc gtVehicleCurveSpringProc(){

float $scale=1;
spaceLocator -p 0 0 0;
string $loc[]=`ls -sl`;
addAttr -ln "gtRadius"  -at double  -min 0.001 -max 1000 -dv 1 -k 1;
addAttr -ln "gtGrow"  -at double  -min 0.001 -max 1000 -dv 1 -k 1;
addAttr -ln "gtSpeed"  -at double  -min 0.001 -max 1000 -dv 1 -k 1;

string $exp="";
    $exp+=$loc[0]+".tx=sin(time*5*"+($loc[0])+".gtSpeed)*"+($loc[0])+".gtRadius*"+$scale+";\n";
    $exp+=$loc[0]+".tz=cos(time*5*"+($loc[0])+".gtSpeed)*"+($loc[0])+".gtRadius*"+$scale+";\n";
    $exp+=$loc[0]+".ty=time*0.5*"+($loc[0])+".gtGrow*"+$scale+";\n";
	expression -s $exp  -ae 1 -uc all -n ($loc[0]+"springLocExp");
	float $p[]=`getAttr ($loc[0]+".translate")`;
	select -cl;
curve -d 1 -p 0 0 0 -p ($p[0]) ($p[1]) ($p[2]) -k 0 -k 1 ;
string $c[]=`ls -sl`;
string $exp="";
    $exp+="if(frame==1){select -r ";
    $exp+=($c[0]);
    $exp+=".cv[1:10000000];\ndelete;}\n";
    $exp+="float $p[]=`getAttr (\""+$loc[0]+".translate\")`;\n";
    $exp+="curve -a  -p ($p[0]) ($p[1]) ($p[2]) (\"";
    $exp+=($c[0]);
    $exp+="\");\n";
	expression -s $exp  -ae 1 -uc all -n ($loc[0]+"gtSpringCurveExp");	
	select -r ($loc[0]);
}
/////////////////////////////

global proc gtVehicleCurveSpringProcA(){
string $curve[]=`ls -sl`;
int $endF=`playbackOptions -q -max`;
if($endF<200)
playbackOptions -e -min 1 -max 200;

for($i=0;$i<size($curve);$i++){
    
spaceLocator -p 0 0 0;
string $locA[]=`ls -sl`;

addAttr -ln "gtRadius"  -at double  -min 0.001 -max 1000 -dv 1 -k 1;
addAttr -ln "gtSpeed"  -at double  -min 0.001 -max 1000 -dv 3 -k 1;
select -r ($locA[0]);
group -n ($locA[0]+"RotGrp");
group -n ($locA[0]+"Grp");

select -r ($locA[0]+"Grp");
select -add ($curve[$i]);

string $animP=`pathAnimation -fractionMode true -follow true -followAxis x -upAxis y -worldUpType "vector" -worldUpVector 0 1 0 -inverseUp false -inverseFront false -bank false -startTimeU 1 -endTimeU 200`;
connectAttr -f ($locA[0]+".gtRadius") ($locA[0]+".translateZ");
string $inputs[]=`listConnections -s 1 -d 0 ($animP+".uValue")`;
select -r $inputs[0];
keyTangent -itt linear -ott linear $inputs[0];

string $exp="";
      $exp+=$locA[0]+"RotGrp.rotateX=frame*10*"+$locA[0]+".gtSpeed;\n";
	expression -s $exp  -ae 1 -uc all -o ($locA[0]) -n ($locA[0]+"springLocExp");
	
spaceLocator -p 0 0 0;
string $loc[]=`ls -sl`;
select -r ($locA[0]);
select -add ($loc[0]);
pointConstraint;
select -r ($locA[0]+"Grp");
select -add ($loc[0]);
group -n ($curve[$i]+"notMoveGrp");
setAttr ($loc[0]+".visibility") 0;
setAttr -lock true ($loc[0]+".visibility");

	currentTime -e 1;
	float $p[]=`getAttr ($loc[0]+".translate")`;
	select -cl;
curve -d 1 -p 0 0 0 -p ($p[0]) ($p[1]) ($p[2]) -k 0 -k 1 ;
string $c[]=`ls -sl`;
string $exp="";
    $exp+="if(frame==1){select -r ";
    $exp+=($c[0]);
    $exp+=".cv[1:10000000];\ndelete;}\n";
    $exp+="float $p[]=`getAttr (\""+$loc[0]+".translate\")`;\n";
    $exp+="curve -a  -p ($p[0]) ($p[1]) ($p[2]) (\"";
    $exp+=($c[0]);
    $exp+="\");\n";
	expression -s $exp  -ae 1 -uc all -o ($c[0]) -n ($loc[0]+"gtSpringCurveExp");	
	select -r ($locA[0]);
    }
}
/////////////////////////
gtSpringCreateWin;
/////////////////////